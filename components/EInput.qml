// EInput.qml
import QtQuick
import QtQuick.Controls
import QtQuick.Layouts
import QtQuick.Effects
import QtQuick.Controls.Basic as Basic

Item {
    id: root
    width: 240
    height: 40

    // === æ¥å£å±æ€§ & ä¿¡å· ===
    property alias text: textField.text
    property alias placeholderText: textField.placeholderText
    property bool readOnly: false
    property bool passwordField: false
    property bool passwordVisible: false
    signal accepted()  // è¾“å…¥å›è½¦è§¦å‘

    // === æ ·å¼å±æ€§ ===
    property int fontSize: 16
    property real radius: 10
    property string showPasswordSymbol: "ğŸ‘"
    property string hidePasswordSymbol: "ğŸ™ˆ"

    // === çŠ¶æ€å±æ€§ ===
    property bool enabled: true
    property bool backgroundVisible: true  // èƒŒæ™¯æ˜¾ç¤ºæ§åˆ¶

    MouseArea {
        id: outsideClickArea
        anchors.fill: parent
        propagateComposedEvents: true
        z: 999
        onPressed: (mouse)=> {
            var pos = mapToItem(root, mouse.x, mouse.y)
            if (!root.contains(pos)) {
                textField.focus = false
            }
            mouse.accepted = false
        }
        preventStealing: true
    }

    property Window windowRef: null

    Component.onCompleted: {
        windowRef = Window.window
        if (windowRef) {
            outsideClickArea.anchors.fill = undefined
            outsideClickArea.parent = windowRef.contentItem
            outsideClickArea.width = windowRef.width
            outsideClickArea.height = windowRef.height
            outsideClickArea.visible = textField.activeFocus
        }
    }

    Connections {
        target: textField
        function onActiveFocusChanged() {
            if (outsideClickArea.parent) {
                outsideClickArea.visible = textField.activeFocus
            }
        }
    }

    Timer {
        id: windowBindTimer
        interval: 100
        repeat: false
        running: !windowRef
        onTriggered: {
            if (Window.window && !windowRef) {
                windowRef = Window.window
                if (windowRef) {
                    outsideClickArea.anchors.fill = undefined
                    outsideClickArea.parent = windowRef.contentItem
                    outsideClickArea.width = windowRef.width
                    outsideClickArea.height = windowRef.height
                    outsideClickArea.visible = textField.activeFocus
                }
            }
        }
    }

    Connections {
        target: windowRef
        function onWidthChanged() {
            if (windowRef && outsideClickArea.parent) {
                outsideClickArea.width = windowRef.width
            }
        }
        function onHeightChanged() {
            if (windowRef && outsideClickArea.parent) {
                outsideClickArea.height = windowRef.height
            }
        }
    }

    // === èƒŒæ™¯ä¸é˜´å½± ===
    MultiEffect {
        source: background
        anchors.fill: background
        shadowEnabled: true
        shadowColor: theme.shadowColor
        shadowBlur: theme.shadowBlur
        shadowHorizontalOffset: theme.shadowXOffset
        shadowVerticalOffset: theme.shadowYOffset
    }

    Rectangle {
        id: background
        anchors.fill: parent
        radius: root.radius
        // æ— èƒŒæ™¯æ—¶ï¼šé€‰ä¸­ç”¨ä¸»é¢˜é«˜äº®è‰²ï¼Œæœªé€‰ä¸­ç”¨æ¬¡çº§è‰²ï¼›æœ‰èƒŒæ™¯æ—¶æ²¿ç”¨ä¸»é¢˜è¾¹æ¡†è‰²
        border.color: root.backgroundVisible 
                       ? theme.getBorderColor(textField.activeFocus)
                       : (textField.activeFocus ? theme.focusColor : theme.textColor)
        border.width: textField.activeFocus ? 2 : 1
        Behavior on border.width { NumberAnimation { duration: 160; easing.type: Easing.OutCubic } }
        Behavior on border.color { ColorAnimation { duration: 150 } }
        color: root.backgroundVisible ? theme.secondaryColor : "transparent"
        opacity: root.enabled ? 1.0 : 0.6
    }

    // === å†…å®¹å¸ƒå±€ ===
    RowLayout {
        id: layout
        anchors.fill: parent
        anchors.margins: 8
        spacing: 6

        // === è¾“å…¥æ¡†ä¸»ä½“ ===
        Basic.TextField {
            id: textField
            Layout.fillWidth: true
            Layout.fillHeight: true
            Layout.alignment: Qt.AlignVCenter

            font.pixelSize: root.fontSize
            color: theme.textColor
            placeholderTextColor: theme.textColor
            readOnly: root.readOnly
            enabled: root.enabled
            verticalAlignment: Text.AlignVCenter
            echoMode: root.passwordField
                      ? (root.passwordVisible ? TextInput.Normal : TextInput.Password)
                      : TextInput.Normal
            background: null
            onAccepted: {
                root.accepted()
                textField.focus = false
            }
            onActiveFocusChanged: {
                if (!activeFocus) {
                    root.accepted()
                }
            }
        }

        // === å¯†ç æ˜¾ç¤ºåˆ‡æ¢æŒ‰é’® ===
        Text {
            id: eyeToggle
            visible: root.passwordField
            text: root.passwordVisible ? root.hidePasswordSymbol : root.showPasswordSymbol
            color: "#666"
            font.pixelSize: 16
            verticalAlignment: Text.AlignVCenter
            horizontalAlignment: Text.AlignHCenter

            MouseArea {
                anchors.fill: parent
                enabled: root.enabled
                cursorShape: Qt.PointingHandCursor
                onClicked: root.passwordVisible = !root.passwordVisible
            }
        }
    }
}
